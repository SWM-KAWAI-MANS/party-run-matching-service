plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.2.1'

}

group = 'online.partyrun'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
    targetCompatibility = 17

}

jib {
    from {
        image = "openjdk:17"
    }
    to {
        tags = ["latest"]
    }
    container {
        jvmFlags = ["-Xms128m", "-Xmx128m"]
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }

    asciidoctorExtensions
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}


task createDocument(type: Copy) {
    dependsOn asciidoctor

    from file("${asciidoctor.outputDir}")
    into file("build/resources/main/static/docs")
}

bootJar {
    dependsOn createDocument
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    //rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'com.github.SWM-KAWAI-MANS:jwt-manager:1.3.0'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    testImplementation('it.ozimov:embedded-redis:0.7.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // mongodb
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring30x:4.7.0'

    // for apple silicon
    developmentOnly 'io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64'

    //mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

}

jar {
    enabled = false
}

jacoco {
    toolVersion = '0.8.8'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

asciidoctor {
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}


jacocoTestReport {
    dependsOn test
    reports {
        html.enabled true
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            // 각 클래스
            element = 'CLASS'

            // 분기문 커버리지
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }

            // 라인 커버리지
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }

            // 코드에서 사용하는 메소드 중, 테스트가 실행된 메소드의 비율
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }

            excludes = [
                    '**.*Application*',
                    '**.*Request*',
                    '**.*Response*',
                    '**.*Config*',
                    '**.*Exception*',
                    '**.*Handler*',
                    '**.*Mapper*',
                    '**.*ControllerAdvice*',
                    '**.WebfluxAuthFilter*',
                    '**.*Event*',
                    '**.external.**',
                    '**.*Advisor*',
                    '**.*MessageListener',
                    '**.*MessagePublisher'
            ]
        }

        rule {
            element = 'METHOD'

            // 최대 method 라인 수
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 20
            }
        }
    }
}